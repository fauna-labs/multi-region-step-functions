// Copyright Fauna, Inc.
// SPDX-License-Identifier: MIT-0
CreateFunction({
  name: "GetNewMessages",
  body: Query(Lambda(
    ["chatId", "requestedLanguage", "lastSeenAt"],
    // Return an array containing:
    // - the messages sent after the Var("lastSeenAt") timestamp
    // - or all messages if Var("lastSeenAt") is null
    // - where .translations.language is equal to Var("requestedLanguage") if available
    // - or send the original message if not or if requestedLanguage is null.
    // Array should look like:
    // [ 
    //   { id: "1", timestamp: "0000001", chat: "1", language: "fr", message: "Bonjour" },
    //   { id: "2", timestamp: "0000002", chat: "1", language: "en", message: "How are you?" }
    // ]
    // NOTE: This uses the default pagination, returning up to 64 messages.
    Map(
      Paginate(
        Range(
          Match(
            Index("MessagesByChat_SortedByTimestamp"),
            [
              Var("chatId")
            ]
          ),
          If(
            IsNull(Var("lastSeenAt")),
            0,
            Var("lastSeenAt")
          ),
          Now()
        )
      ),
      Lambda(
        "match",
        Let(
          {
            timestamp: Select(0, Var("match")),
            messageID: Select(1, Var("match")),
            chat: Select(2, Var("match")),
            originalText: Select(3, Var("match")),
            originalLanguage: Select(4, Var("match")),
            translatedMessage: Match(
              Index("TranslationsByMessageAndLanguage"),
              [
                Ref(Collection("Messages"), Var("messageID")),
                Var("requestedLanguage")
              ]
            ),
            // Because there should only be one translation for a message/language
            // pair, we can use Get() to return the "first" (only) message in the
            // set. If the set is empty, we instead use the text and language from
            // the original message.
            message: If(
              IsNonEmpty(Var("translatedMessage")),
              Select(["data"], Get(Var("translatedMessage"))),
              {
                text: Var("originalText"),
                language: Var("originalLanguage")
              }
            )
          },
          {
            id:         Var("messageID"),
            timestamp:  Var("timestamp"),
            chat:       Var("chat"),
            text:       Select(["text"], Var("message")),
            language:   Select(["language"], Var("message")),
          }
        )
      )
    )
  ))
})

// Shape of a Message:
// {
//   chat: "1234567890",
//   originalText: "A string",
//   originalLanguage: "en",
// }

// Shape of a Translation:
// {
//   chat: Ref(Collection("Chats"), "1234567890"),
//   originalText: "A string",
//   originalLanguage: "en",
// }
