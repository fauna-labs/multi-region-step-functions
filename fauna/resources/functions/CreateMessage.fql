// Copyright Fauna, Inc.
// SPDX-License-Identifier: MIT-0

CreateFunction({
  name: "CreateMessage",
  body: Query(Lambda(
    ["chat", "language", "text"],
    Let({
      // First, store the original message for future translations.
      newMessage: Create(
        Collection("Messages"), 
        {
          data: {
            chat: Var("chat"),
            originalLanguage: Var("language"),
            originalText: Var("text"),
          }
        }
      ),
      // Next, store the original message as a "translation" for retrieval.
      translation: Create(
        Collection("Translations"),
        {
          data: {
            message: Select(["ref"], Var("newMessage")),
            language: Var("language"),
            text: Var("text"),
          }
        }
      )
    },
    // Return the relevant data from the stored message.
    {
      id: Select(["ref", "id"], Var("newMessage")),
      timestamp: Select(["ts"], Var("newMessage")),
      chat: Select(["data", "chat"], Var("newMessage")),
      originalLanguage: Select(["data", "originalLanguage"], Var("newMessage")),
      originalText: Select(["data", "originalText"], Var("newMessage")),
    })
  ))
})

// Shape of a Message:
// {
//   chat: "1234567890",
//   originalLanguage: "en",
//   originalText: "A string",
// }

// Shape of a Translation:
// {
//   message: Ref(Collection("Messages"), "1234567890"),
//   language: "en",
//   text: "A string",
// }
